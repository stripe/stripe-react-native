buildscript {
    apply(from: {
        def searchDir = rootDir.toPath()
        do {
            def p = searchDir.resolve("node_modules/react-native-test-app/android/dependencies.gradle")
            if (p.toFile().exists()) {
                return p.toRealPath().toString()
            }
        } while (searchDir = searchDir.getParent())
        throw new GradleException("Could not find `react-native-test-app`");
    }())

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        getReactNativeDependencies().each { dependency ->
            classpath(dependency)
        }
    }
}

allprojects {
    repositories {
        {
            def searchDir = rootDir.toPath()
            do {
                def p = searchDir.resolve("node_modules/react-native/android")
                if (p.toFile().exists()) {
                    maven {
                        url = p.toRealPath().toString()
                    }
                    break
                }
            } while (searchDir = searchDir.getParent())
            // As of 0.80, React Native is no longer installed from npm
        }()
        maven { url uri("$rootDir/privateMavenRepository") }
        mavenCentral()
        google()
    }
}

// Fix MainActivity launchMode and add deep link for stripe-connect
subprojects { subproject ->
    subproject.afterEvaluate {
        subproject.tasks.matching { task ->
            task.name == 'generateAndroidManifest'
        }.configureEach { task ->
            task.doLast {
                def manifestFile = file("${subproject.buildDir}/generated/rnta/src/main/AndroidManifest.xml")
                if (manifestFile.exists()) {
                    def manifest = manifestFile.text

                    // Add launchMode="singleTask"
                    manifest = manifest.replace(
                        '<activity android:name="com.microsoft.reacttestapp.MainActivity" android:exported="true">',
                        '<activity android:name="com.microsoft.reacttestapp.MainActivity" android:exported="true" android:launchMode="singleTask">'
                    )

                    // Add deep link intent filter for stripe-connect
                    def intentFilterXml = '''<intent-filter>
        <action android:name="android.intent.action.VIEW"></action>
        <category android:name="android.intent.category.DEFAULT"></category>
        <category android:name="android.intent.category.BROWSABLE"></category>
        <data android:scheme="stripe-connect"></data>
      </intent-filter>
      '''
                    manifest = manifest.replace(
                        '</intent-filter>\n    </activity>',
                        '</intent-filter>\n      ' + intentFilterXml + '</activity>'
                    )

                    manifestFile.text = manifest
                    println "âœ“ Updated MainActivity: launchMode=singleTask + stripe-connect deep link"
                }
            }
        }
    }
}
